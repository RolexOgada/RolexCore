plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
}

android {
    namespace 'com.collince.rolexcore.library'
    compileSdk 34

    defaultConfig {
        minSdk 24
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

// Maven Publish Configuration
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Use release build output
                from components.findByName('release')

                // Explicitly add the .aar file from libs directory
                artifact("libs/rolexecore-release.aar") {
                    builtBy tasks.named("assembleRelease")
                }

                groupId = 'com.collince'
                artifactId = 'rolexcore'
                version = '1.1.4'

                pom {
                    name.set("RolexCore Library")
                    description.set("A library for core functionality in Rolex projects")
                    url.set("https://github.com/RolexOgada/rolexcore")
                    licenses {
                        license {
                            name.set("MIT License")
                            url.set("https://opensource.org/licenses/MIT")
                        }
                    }
                    developers {
                        developer {
                            id.set("rolexogada")
                            name.set("Rolex Ogada")
                            email.set("rolexogada@gmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/RolexOgada/rolexcore.git")
                        developerConnection.set("scm:git:ssh://git@github.com/RolexOgada/rolexcore.git")
                        url.set("https://github.com/RolexOgada/rolexcore")
                    }
                }
            }
        }
    }
}

// Ensure assembleRelease is triggered during JitPack build
tasks.configureEach { task ->
    if (task.name == "assemble") {
        task.dependsOn assembleRelease
    }
}
